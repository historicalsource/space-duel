	.TITLE AS2MSG-ASTERIODS 2 MESSAGES
	.CSECT AS2MSG
	.RADIX 16
	.LIST MEB			;LIST ASCIN EXPANSION
	.NLIST BYT
	.NOCROSS ..1,..2,..3,..4,..C,..N
;********************************
;*
;*THIS PROGRAM HANDLES ALL MESSAGES FOR THE GAME OF ASTEROIDS II.
;*SEE AST2RD.MAC FOR HARDWARE CONFIGURATION.
;*
;********************************
	.SBTTL ******************
	.SBTTL *
	.SBTTL *PROGRAMMER: OWEN RUBIN
	.SBTTL *
	.SBTTL * MODELLED AFTER ED LOGG'S
	.SBTTL * DISK 77 & B49
	.SBTTL ******************
;
;ENTRY POINTS
	.GLOBL VGMSG,VGMSG5,VGMSG3,VGMSG4	;MESSAGE PROCESSOR
	.GLOBL MESGPOS,LANGOFF
;
;ZERO PAGE DECLARATIONS
;
	.GLOBB TEMP1,VGLIST,TEMP2,OPTN1,LANG
	.GLOBL UPDOWN
;
;EXTERNAL ENTRY POINTS
;
	.GLOBL VGVTR5,VGADD2
	.GLOBL VGVTR,VGADD,VGSCA1,VGCNTR,VGSTAT	;VECTOR UTILITY ROUTINES
	.GLOBL VGMSGA,VGMSGU,L0,L02		;JSRL CHARACTER TABLE
	.GLOBL CNTR8,CNTR9
	.NLIST
	.INCLUDE AS2DEC	;<<<<<<<<<<<<<<<<<<<<<<<<
	.INCLUDE VGMC	;<<<<<<<<<<<<<<<<<<<<<<<<<
	.LIST
;
;************************************************************
;* ABOVE THIS THERE ARE 2 INCLUDES..........................*
;* ONE IS FOR VGMC, THE OTHER FOR AS2DEC.                   *
;************************************************************
;ASCIN - TAKE AN ASCII STRING AND MAP 3 CHARACTERS TO 2 BYTES
;
;THIS MACRO PACKS 3 ASCII CHARACTERS (BLANK, A THRU Z,0,1 OR 2) INTO 2
;BYTES IN THE FORMAT
;	AAAAABBB BBCCCCCD
;WHERE D=1 MEANS END OF LIST
;	AAAAA,BBBBB,CCCCC=INDEX AS FOLLOWS
;	0=END OF LIST
;	1=BLANK
;	2=0
;	3=1
;	4=2
;	5=A
;	ETC.
	.MACRO ASCIN STRING
	..N=0
	.NCHR ..C,<STRING>
	.ENABL M68
	.IRPC ..5,<STRING>
	..4=''..5
	..C=..C-1
	..3=0
	.IIF EQ,..4-^H20,..3=1
	.IF GE,..4-^H30
	.IIF LT,..4-^H33,..3=..4-^H30+2
	.ENDC
	.IF GE,..4-^H41
	.IIF LT,..4-^H5B,..3=..4-^H41+5
	.ENDC
	.IIF EQ,..N,..1=..3
	.IIF EQ,..N-1,..2=..3
	..N=..N+1
	.IF EQ,..N-3
	..N=0
	.IIF EQ,..C,.WORD <..1*^H800>+<..2*^H40>+<..3*2>+1
	.IIF NE,..C,.WORD <..1*^H800>+<..2*^H40>+<..3*2>
	.ENDC
	.ENDM
	.IIF EQ,..N-1,.WORD ..1*^H800
	.IIF EQ,..N-2,.WORD <..1*^H800>+<..2*^H40>
	.DSABL M68
	.ENDM
	
	
	.SBTTL VGMSG-VECTOR GENERATOR MESSAGE PROCESSOR
	.SBTTL	POSITION ROUTINE
;
; PASS XPOS=A, YPOS=X
; WILL DO A CENTER THEN A CLEAR VECTOR TO POSITION
;
; AUX ROUTINE TO ADD OFFSET FOR FOREIGN LANGUAGES
LANGOFF:
; ADD OFFSET IN X DIR ONLY 
; ENTER X=OFFSET INDEX....THIS ROUTINE CHECKS LANG
; ALL MESSAGES ASSUMED CORRECT FOR ENGLISH
	LDA LANG
	BNE 10$
	RTS
10$:	TXA
	ASL
	ASL
	CLC
	ADC LANG
	TAY
	LDA Y,OFFSET
	LDX #0
	JMP VGVTR5
OFFSET:	.BYTE 0,-0B,-0B,-0B
	.BYTE 0,-20,-20,-08
	.BYTE 0,-1B,-0A,-0B
	.BYTE 0,-12,-12,-20
	.BYTE 0,1A,-08,3D
	.BYTE 0,-0E,-0E,08
	.BYTE 0,-16,0EE,0
	.PAGE

MESGPOS:
	PHA			;SAVE X & Y
	TXA
	PHA
	LAL CNTR9
	LXH CNTR8		;CENTER SCALE
	JSR VGADD2
	PLA
	TAX
	PLA
	JMP VGVTR5		;POSITION
;VGMSG-VECTOR GENERATOR MESSAGE PROCESSOR
;
;ENTRY	(Y)=MESSAGE NUMBER (0,1,2,...)
;(X)=BRIGHTNESS
;USES	A,X,Y(TEMP1,TEMP1+1),TEMP2,(VGLIST,VGLIST+1)
VGMSG:	LDX #0D6		;DEFAULT COLOR IS YELLOW
VGMSG3:	TXA			;BRIGHTNESS
VGMSG4:	STY TEMP2		;PASS COLOR IN A
	TAY
	JMP VGMSG7
VGMSG5:
	STY TEMP2
	LDY #0D2		;GREEN MESSAGES
VGMSG7:	JSR VGSTAT
	LDY TEMP2
VGMSG6:
;MESSAGES MAY OVERFOW ONE PAGE
	LDA LANG		;READ OPTIONS
	ASL
	ASL			;LANG *4
	CPY I,21.
	BCC 5$			;NO OVERFLOW
	ADC I,2-1		;ADD TWO
	TAX
	TYA
	SEC
	SBC I,21.		;REDUCE RELATIVE MESSAGE NUMBER
	TAY
	BPL 7$			;ALWAYS
5$:	TAX			;4*LANGUAGE (0,4,8,OR 12.)
7$:	LDA AX,VGMSGT+1
	STA TEMP1+1
	LDA AX,VGMSGT		;CARRY IS CLEAR FROM ASL ABOVE
	STA TEMP1		;TEMP1 SETUP NOW
	CLC
	ADC NY,TEMP1		;RELATIVE ADDRESS TO START OF MESSAGE
	STA TEMP1
	BCC 10$			;NO OVERFLOW
	INC TEMP1+1
10$:
	LDY I,0			;Y DOUBLES AS INDEX FOR VGLIST AND TEMP1
	LDX I,0
20$:	LDA NX,TEMP1
	STA TEMP2
	LSR
	LSR			;2*INDEX
	JSR VGMSG1		;PUT OUT CHARACTER AND UPDATE TEMP1
	LDA NX,TEMP1
	ROL
	ROL TEMP2
	ROL
	LDA TEMP2
	ROL
	ASL
	JSR VGMSG2		;PUT OUT CHARACTER
	LDA NX,TEMP1
	STA TEMP2
	JSR VGMSG1		;PUT OUT CHARACTER AND UPDATE TEMP1
	LSR TEMP2
	BCC 20$			;NOT END OF LIST
VGMSG0:	DEY
	JMP VGADD		;UPDATE VGLIST POINTER

VGMSG1:	INC TEMP1		;UPDATE INDIRECT POINTER TO CHARACTERS
	BNE VGMSG2		;NO OVERFLOW
	INC TEMP1+1
VGMSG2:	AND I,3E
	BNE 5$			;NOT END OF LIST
	PLA
	PLA			;PURGE RTS
	BNE VGMSG0		;RETURN
	
5$:	CMP I,10.
	BCC 10$			;IF BLANK, 0,1 OR 2
	ADC I,13.		;SET CORRECT INDEX (ADD 14.)
10$:	TAX
	LDA A,UPDOWN
	ASL			;SET CARRY
	LDA AX,VGMSGA-2		;10. FOR A, 12. FOR B, ....
	BCC 20$			;NORMAL
	LDA AX,VGMSGU-2		;UPSIDE DOWN LETTERS
20$:	STA NY,VGLIST		;PUT JSRL INTO VECTOR LIST
	INY
	LDA AX,VGMSGA-1
	BCC 30$			;NORMAL
	LDA AX,VGMSGU-1
30$:	STA NY,VGLIST
	INY
	LDX I,0
	RTS
VGMSGT:	.WORD L0		;LANGUAGE TABLE POINTERS (SEE OPTSW 1)
	.WORD L02
	.WORD L1
	.WORD L12
	.WORD L2
	.WORD L22
	.WORD L3
	.WORD L32
L0:	.BYTE 10$-L0,11$-L0,12$-L0,13$-L0,14$-L0
	.BYTE 15$-L0,16$-L0,17$-L0,18$-L0,19$-L0
	.BYTE 20$-L0
	.BYTE 21$-L0,22$-L0
	.BYTE 51$-L0,52$-L0,53$-L0
	.BYTE 55$-L0,56$-L0,57$-L0,58$-L0
	.BYTE 59$-L0
10$:	ASCIN ^/HIGH SCORES/
11$:	ASCIN ^/PLAYER /
12$:	ASCIN ^/GREAT SCORE/
13$:	ASCIN ^/PLEASE ENTER YOUR INITIALS/
14$:	ASCIN ^/PRESS ROTATE TO SELECT LETTER/
15$:	ASCIN ^/PRESS SHIELDS WHEN LETTER IS CORRECT/
16$:	ASCIN ^/ SELECT GAME/
17$:	ASCIN ^/GAME OVER/
18$:	ASCIN ^/1 COIN 2 PLAYERS/
19$:	ASCIN ^/1 COIN 1 PLAYER/
20$:	ASCIN ^/2 COINS 1 PLAYER/
21$:	ASCIN ^/1 PLAYER/
22$:	ASCIN ^/2 PLAYERS/
51$:	ASCIN ^/ PRESS START/	
52$:	ASCIN ^/BONUS EVERY /
53$:	ASCIN ^/COMBINED SCORE/
55$:	ASCIN ^/NORMAL/
56$:	ASCIN ^/MEDIUM/
57$:	ASCIN ^/2 COIN MINIMUM/
58$:	ASCIN	^/ANOTHER COIN/
59$:	ASCIN ^/BONUS LEVEL /
L02:	.BYTE 10$-L02,11$-L02,12$-L02,13$-L02,14$-L02
	.BYTE 17$-L02,18$-L02,19$-L02
	.BYTE 20$-L02,21$-L02,22$-L02,23$-L02,24$-L02
	.BYTE 25$-L02,26$-L02,27$-L02,28$-L02
10$:	ASCIN ^/1 COIN 2 GAMES/
11$:	ASCIN ^/1 COIN 1 GAME/
12$:	ASCIN ^/2 COINS 1 GAME/
13$:	ASCIN ^/CREDITS /
14$:	ASCIN ^/EASY/
17$:	ASCIN ^/SECONDS/
18$:	ASCIN ^/GAMES/
19$:	ASCIN ^/AVG TIMES/
20$:	ASCIN ^/FOR SELF TEST/
21$:	ASCIN ^/TO CLEAR SCORES/
22$:	ASCIN ^/TO CLEAR TIMES/
23$:	ASCIN ^/TO CLEAR TIMES AND SCORES/
24$:	ASCIN ^/ERASING/
25$:	ASCIN ^/PUSH START AND SELECT/
26$:	ASCIN ^/BONUS ADDER/
27$:	ASCIN ^/TOTAL ON TIME/
28$:	ASCIN ^/HARD/
;********************************************************************************
;*		GERMAN								*
;***********************************************************************************
L1:	.BYTE 10$-L1,11$-L1,12$-L1,13$-L1,14$-L1
	.BYTE 15$-L1,16$-L1,17$-L1,18$-L1,19$-L1
	.BYTE 20$-L1,21$-L1,22$-L1,51$-L1,52$-L1,53$-L1
	.BYTE 55$-L1,56$-L1,57$-L1,58$-L1
	.BYTE 59$-L1
10$:	ASCIN	^/HOECHSTERGEBNISSE/
11$:	ASCIN	^/SPIELER /
12$:	ASCIN	^/GROSSARTIGES ERGEBNIS/
13$:	ASCIN	^/BITTE GEBEN SIE IHRE INITIALEN EIN/
14$:	ASCIN	^/ZUR BUCHSTABENWAHL ROTATE DRUECKEN/
15$:	ASCIN	^/FIRE DRUECKEN WENN RICHTIG/
16$:	ASCIN	^/     SPIELWAHL     /
17$:	ASCIN	^/SPIELENDE/
18$:	ASCIN	^/1 MUENZE 2 SPIELER/
19$:	ASCIN	^/1 MUENZE 1 SPIELER/
20$:	ASCIN	^/2 MUENZEN 1 SPIELER/
21$:	ASCIN 	^/1 SPIELER/
22$:	ASCIN	^/2 SPIELER/
51$:	ASCIN	^/STARTKNOPF DRUECKEN/
52$:	ASCIN	^/BONUS JEDE /
53$:	ASCIN	^/  TEAM PUNKTE /
55$:	;EASY, MEDIUM MESSAGES HAVE THESE PLACES
56$:
57$:	ASCIN 	^/MINDESTENS 2 MUENZEN/
58$:	ASCIN	^/  NOCH EINE MUENZE /
59$:	ASCIN	^/PUNKTZAHLEN ERHOEHT UM /
L12:	.BYTE 10$-L12,11$-L12,12$-L12,13$-L12
10$:	ASCIN	^/1 MUENZE 2 SPIELE/
11$:	ASCIN	^/1 MUENZE 1 SPIEL/
12$:	ASCIN	^/2 MUENZEN 1 SPIEL/
13$:	ASCIN	^/KREDITE /
14$:	.BYTE 0		;SUPER EASY MESSAGE GOES HERE
;*******************************************************************************
;*		FRENCH								*
;***********************************************************************************
L2:	.BYTE 10$-L2,11$-L2,12$-L2,13$-L2,14$-L2
	.BYTE 15$-L2,16$-L2,17$-L2,18$-L2,19$-L2
	.BYTE 20$-L2,21$-L2,22$-L2,51$-L2,52$-L2,53$-L2
	.BYTE 55$-L2,56$-L2,57$-L2,58$-L2
	.BYTE 59$-L2
10$:	ASCIN	^/MEILLEURS SCORE/
11$:	ASCIN	^/JOUEUR /
12$:	ASCIN	^/SPLENDIDE SCORE/
13$:	ASCIN	^/SVP ENTREZ VOS INITIALES/
14$:	ASCIN	^/POUSSEZ ROTATE POUR VOS INITIALES/
15$:	ASCIN	^/POUSSEZ SHIELDS QUAND LETTRE CORRECTE/
16$:	ASCIN	^/  CHOIX DU JEU   /
17$:	ASCIN	^/FIN DE PARTIE/
18$:	ASCIN	^/1 PIECE 2 JOUEURS/
19$:	ASCIN	^/1 PIECE 1 JOUEUR/
20$:	ASCIN	^/2 PIECES 1 JOUEUR/
21$:	ASCIN	^/1 JOUEUR/
22$:	ASCIN	^/2 JOUEURS/
51$:	ASCIN 	^/APPUYEZ SUR START/
52$:	ASCIN	^/BONUS CHAQUE /
53$:	ASCIN 	^/SCORE COMBINE/
55$:	;NORMAL DIFF MESSAGE
56$:	;MEDIUM  "     "
57$:	ASCIN	^/DUEX PIECES MINIMUM/
58$:	ASCIN	^/ UNE AUTRE PIECE /
59$:	ASCIN	^/     BONUS /
L22:	.BYTE 10$-L22,11$-L22,12$-L22,13$-L22
10$:	ASCIN	^/1 PIECE 2 JOUEURS/
11$:	ASCIN 	^/1 PIECE 1 JOUEUR/
12$:	ASCIN	^/2 PIECES 1 JOUEUR/
13$:	ASCIN	^/CREDITS /
;*******************************************************************************
;*		SPANISH								*
;*************************************************************************************
L3:	.BYTE 10$-L3,11$-L3,12$-L3,13$-L3,14$-L3
	.BYTE 15$-L3,16$-L3,17$-L3,18$-L3,19$-L3
	.BYTE 20$-L3,21$-L3,22$-L3,51$-L3,52$-L3,53$-L3
	.BYTE 55$-L3,56$-L3,57$-L3,58$-L3
	.BYTE 59$-L3
10$:	ASCIN	^/  RECORDS  /
11$:	ASCIN	^/JUGADOR /
12$:	ASCIN	^/GRAN PUNTAJE/
13$:	ASCIN	^/POR FAVOR ENTRE SUS INICIALES/
14$:	ASCIN	^/OPRIMA ROTATE PARA SELECCIONAR LA LETRA/
15$:	ASCIN	^/OPRIMA SHIELDS/
16$:	ASCIN	^/ELEGIR JUEGO/
17$:	ASCIN	^/JUEGO TERMINADO/
18$:	ASCIN	^/1 FICHA 2 JUGADORES/
19$:	ASCIN	^/1 FICHA 1 JUGADOR/
20$:	ASCIN	^/2 FICHAS 1 JUGADOR/
21$:	ASCIN	^/1 JUGADOR/
22$:	ASCIN	^/2 JUGADORES/
51$:	ASCIN	^/PULSAR START/
52$:	ASCIN	^/EXTRA CADA /
53$:	ASCIN	^/SCORE DE AMBOS/
55$:	;MESSAGE NORMAL
56$:	;MESSAGE MEDIUM
57$:	ASCIN	^/DOS FICHAS MINIMO/	
58$:	ASCIN	^/ OTRA FICHA /
59$:	ASCIN	^/NIVEL DE BONIFICTION/
L32:	.BYTE 10$-L32,11$-L32,12$-L32,13$-L32
10$:	ASCIN	^/1 FICHA 2 JUEGOS/
11$:	ASCIN	^/1 FICHA 1 JUEGO/
12$:	ASCIN	^/2 FICHAS 1 JUEGO/
13$:	ASCIN	^/CREDITOS /
	.END
                                                                                                                                        